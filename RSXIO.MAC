.INCLUDE /DECDFN/
.MODULE DECIO,RELEASE=V05,VERSION=01,COMMENT=<ОБЩИЕ П/П В/В>

	.MCALL	DIR$	QIOW$	PUT$	GET$	CLOSE$
	.MCALL	OFNB$	READ$	FDOF$L	ASTX$S	STOP$S

	FDOF$L

............................................................
;
; WR$LIN - ЗАПИСЬ В ВЫХОДНОЙ ФАЙЛ
; ПАРАМЕТРЫ:
; CURLIN - СТРОКА ДЛЯ ВЫВОДА
; В R2 - АДРЕС КОНЦА СТРОКИ
;
............................................................
WR$LIN::
	JSR	R1,$SAV2
	MOV	#CURLIN,R1
	SUB	R1,R2		;ВЫЧИСЛИТЬ ДЛИНУ
; УДАЛЕНИЕ СИМВОЛОВ - ЗАПОЛНИТЕЛЕЙ <200>
	MOV	R1,R0
1$:	MOVB	(R1)+,@R0
	CMPB	(R0)+,#200	; СИМВОЛ - ЗАПОЛНИТЕЛЬ?
	BNE	4$		; НЕТ
	DEC	R0		; ПРОПУСТЬ СИМВОЛЫ-ЗАПОЛНИТЕЛИ
4$:	SOB	R2,1$

	TST	TTYFLG		;ВЫВОД НА ТЕРМИНАЛ?
	BEQ	2$		;НЕТ
	MOVB	#15,(R0)+	; ЗАВЕРШИТЬ СТРОКУ <CR><LF>
	MOVB	#12,(R0)+
	SUB	#CURLIN,R0	; ВЫЧИСЛИТЬ НОВУЮ ДЛИНУ СТРОКИ ДЛЯ ВЫВОДА
	MOV	R0,TTOQIO+Q.IOPL+2
	DIR$	#TTOQIO
	RETURN

2$:	SUB	#CURLIN,R0	; ВЫЧИСЛИТЬ НОВУЮ ДЛИНУ СТРОКИ ДЛЯ ВЫВОДА
	MOV	R0,FDBOUT+F.NRBD ; РАЗМЕР ЗАПИСИ
	PUT$	#FDBOUT
	BCS	3$
	RETURN
3$:	FATAL	E.OUT
............................................................
;
; WR$TTY - ВЫВЕСТИ СТРОКУ НА ТЕРМИНАЛ, R0 - АДРЕС СТРОКИ
;
............................................................
WR$TTY::
	MOV	R1,-(SP)
	MOV	R0,TTYQIO+Q.IOPL	; АДРЕС СТРОКИ
	MOV	R0,R1
1$:	BITB	#177,(R0)+		; НАШЛИ КОНЕЦ СТРОКИ
	BNE	1$
	MOVB	-(R0),-(SP)		; СОХРАНИМ ПОСЛЕДНИЙ СИМВОЛ
	SUB	R1,R0
	MOV	R0,TTYQIO+Q.IOPL+2	; ДЛИНА
	DIR$	#TTYQIO			; ВЫВЕСТИ НА ТЕРМИНАЛ
	TSTB	(SP)+
	BMI	2$
	DIR$	#TTCRLF			; ВЫВЕСТИ ЗАВ. <CR><LF>
2$:	MOV	(SP)+,R1
	RETURN
............................................................
;
; TS$TTY - ОПРОСИТЬ , НЕ БЫЛО ЛИ ПРЕРЫВАНИЯ С ТЕРМИНАЛА
; 'C' SET - НЕ БЫЛО
............................................................
TS$TTY::
	TST	CCFLAG
	BEQ	1$
	CLR	CCFLAG
	TST	(PC)+
1$:	SEC
	RETURN
............................................................
;
; RD$PRM - СЧИТАТЬ СТРОКУ С ТЕРМИНАЛА В CMDLIN С ПОДСКАЗКОЙ
;
............................................................
RD$PRM::
	MOV	R1,-(SP)
	MOV	#CMDLIN,R1
1$:	CMPB	(R1)+,#200
	BNE	1$
	SUB	#CMDLIN+1,R1
	MOV	R1,PRMQIO+Q.IOPL+10
	DIR$	#PRMQIO
	MOV	IOSB+2,R0
	ADD	#CMDLIN,R0	; НА КОНЕЦ ВВЕД. СТРОКИ
	CMPB	#IE.EOF,IOSB	; ВВЕДЕНО CTRL/Z
	BNE	2$		; НЕТ
	MOVB	#'Z-100,(R0)+	; CTRL/Z
	BR	3$
2$:	CMPB	IOSB+1,#15	; ОКОНЧАНИЕ СТРОКИ ПО <CR>
	BEQ	3$		; ДА, СТАНДАРТНОЕ ОКОНЧАНИЕ
	MOVB	IOSB+1,(R0)+
3$:	CLRB	@R0
	DIR$	#TTCRLF			; ВЫВЕСТИ ЗАВ. <CR><LF>
	MOV	(SP)+,R1
	CLR	CCFLAG
	RETURN
............................................................
;
; СЧИТАТЬ БЛОК ИЗ ФАЙЛА.
;
............................................................
RD$BLK::
	MOV	R0,MEMBLK
	INC	R0
	MOV	R0,QIOBLK+Q.IOPL+10
	DIR$	#QIOBLK
	BCS	1$
	CMP	IOSB,#IS.SUC
	BEQ	2$
1$:	FATAL	E.INP
2$:	RETURN
............................................................
;
; RL$LIN - СЧИТАТЬ СТРОКУ В CURLIN , ЗАКОНЧИТЬ 0
;
............................................................
RD$LIN::
	GET$	#FDBIN
	BCS	1$
	MOV	F.NRBD(R0),R0
	ADD	#CURLIN,R0
	CLRB	@R0
	RETURN
1$:	CLRB	CURLIN
	RETURN
............................................................
;
; OP$MAC - ОТКРЫТЬ ВЫХОДНОЙ ФАЙЛ .MAC
;
............................................................
OP$MAC::
	MOV	#FDBOUT,R0
	MOV	#MACFNB,R1
	BR	OP$FIL
............................................................
;
; OP$DIA - ОТКРЫТЬ .DIA - ФАЙЛ ДЛЯ ВЫВОДА
;
............................................................
OP$DIA::
	MOV	#FDBOUT,R0
	MOV	#OUDFNB,R1
	BR	OP$FIL
............................................................
;
; IN$IMG - ОТКРЫТЬ ВХОДНОЙ ФАЙЛ .TSK
;
............................................................
IN$IMG::
	MOV	#-1,MEMBLK
	MOV	#IMGFDB,R0
	MOV	#IMGFNB,R1
	BR	OP$FIL
............................................................
;
; IN$DIA - ОТКРЫТЬ ФАЙЛ .DIA ДЛЯ ВВОДА
;
............................................................
IN$DIA::
	MOV	#FDBIN,R0
	MOV	#INDFNB,R1
OP$FIL:	MOV	R0,R2
	ADD	#F.FNB,R2
	MOV	#S.FNBW,R3
1$:	MOV	(R1)+,(R2)+	; ПЕРЕПИСАТЬ ИМЯ ФАЙЛА ДЛЯ ОТКРЫТИЯ
	SOB	R3,1$
	OFNB$			; ОТКРЫТЬ ФАЙЛ
	RETURN			; ВОЗВРАТ

............................................................
;
; CL$MAC - ДОВЫВОД И ЗАКРЫТИЕ ВЫХОДНОГО ФАЙЛА
;
............................................................
CL$MAC::
............................................................
;
; CL$DIA - ДОВЫВОД И ЗАКРЫТИЕ ВЫХОДНОГО .DIA - ФАЙЛА
;
............................................................
CL$DIA::
	MOV	#FDBOUT,R0
	BR	CL$FIL
............................................................
;
; PR$IMG - ЗАКРЫТИЕ ВХОДНОГО ФАЙЛА .TSK
;
............................................................
PR$IMG::
	MOV	#IMGFDB,R0
	BR	CL$FIL
............................................................
;
; PR$DIA - ЗАКРЫТИЕ ВХОДНОГО .DIA - ФАЙЛА
;
............................................................
PR$DIA::
	MOV	#FDBIN,R0
CL$FIL:	TST	F.BDB(R0)	; ФАЙЛ ОТКРЫТ?
	BEQ	1$		; НЕТ
	CLOSE$
1$:	RETURN
............................................................
;
; УСТАНОВИТЬ AST ПО НЕЗАПЛАНИРОВННОМУ ВВОДУ С ТЕРМИНАЛА
;
............................................................
SETAST::
	DIR$	#ATTTTY
	RETURN
............................................................
;
; CCAST - AST ПО ВВОДУ НЕЗАПЛАНИРОВАННОГО СИМВОЛА С ТЕРМИНАЛА
;
............................................................
CCAST:
	MOV	SP,CCFLAG
	TST	(SP)+
	ASTX$S
............................................................
;
; ВРЕМЕННАЯ ОСТАНОВКА ЗАДАЧИ
;
............................................................
$PAUSE::
	DIR$	#DETTTY
	STOP$S
	DIR$	#ATTTTY
	RETURN
............................................................
;
; ЗАКРЫТЬ ФАЙЛЫ, ОТКРЕПИТЬ ТЕРМИНАЛ
;
............................................................
IO$RST::
	MOV	#FDBOUT,R0
	TST	F.BDB(R0)
	BEQ	1$
	CALL	.DLFNB		; ЗАКРЫТЬ С УДАЛЕНИЕМ ВЫХОДНОЙ ФАЙЛ
1$:	CALL	PR$IMG		; ЗАКРЫТЬ ВХОДНОЙ ФАЙЛ ЗАДАЧИ
	CALL	PR$DIA		; ЗАКРЫТЬ ВХОДНОЙ ФАЙЛ ДИАЛОГА
	DIR$	#DETTTY		; ОТКРЕПИТЬ ТЕРМИНАЛ
	RETURN
............................................................
;
; БЛОЧНЫЕ БУФЕРА И УКАЗАТЕЛИ ДЛЯ ВВОДА/ВЫВОДА
;
............................................................
	.PSECT	QIO,D,RW
TTOQIO:	QIOW$	IO.WAL,TILUN,TIEFN,,IOSB,,<CURLIN,80.>
PRMQIO:	QIOW$	IO.RPR!TF.BIN,TILUN,TIEFN,,IOSB,,<CMDLIN,80.,,CMDLIN,80.,0>
TTYQIO:	QIOW$	IO.WAL,TILUN,TIEFN,,IOSB
TTCRLF::QIOW$	IO.WAL,TILUN,TIEFN,,IOSB,,<CRLF,2>
DETTTY::QIOW$	IO.DET,TILUN,TIEFN
ATTTTY:	QIOW$	IO.ATA,TILUN,TIEFN,,IOSB,,<CCAST>
QIOBLK::QIOW$	IO.RVB,IMGLUN,IMGEFN,,IOSB,,<INBUF,512.>
CRLF:	.BYTE	15,12
	.PSECT	DECDAT,D
CCFLAG::.BLKW	1
IOSB:	.BLKW	2
INBUF:: .BLKB	512.	; БЛОЧНЫЙ БУФФЕР ВХОДНОГО ФАЙЛА

	.END
